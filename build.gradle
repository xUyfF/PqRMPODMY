buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'
apply plugin: 'maven'

version = "2.5.0"
group = "org.cloudfoundry"
description = "Spring Boot project for creating Cloud Foundry service brokers"

archivesBaseName = "spring-boot-cf-service-broker"

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    springBootVersion = '1.2.2.RELEASE'
    hibernateValidatorVersion = '5.1.0.Final'
    jsonPathVersion = '0.9.1'

    baseNameTests = "${project.archivesBaseName}-tests"
}

task testsJar(type: Jar) {
    from sourceSets.test.output
    baseName = baseNameTests
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    compile("org.hibernate:hibernate-validator:${hibernateValidatorVersion}")
    compile("commons-beanutils:commons-beanutils:1.9.2")

    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("com.jayway.jsonpath:json-path:${jsonPathVersion}")
}

publishing {
    publications {
        jar(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java
        }
        test(MavenPublication) {
            artifactId baseNameTests
            artifact testsJar
        }
        sources(MavenPublication) {
            artifact sourcesJar {
                classifier "sources"
            }
            from components.java
        }
        javadoc(MavenPublication) {
            artifact javadocJar {
                classifier "javadoc"
            }
            from components.java
        }
    }
    if (project.hasProperty("artifactory_contextUrl")) {
        repositories {
            maven {
                credentials {
                    username "${artifactory_user}"
                    password "${artifactory_password}"
                }
                url "${artifactory_contextUrl}/libs-release-local"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
